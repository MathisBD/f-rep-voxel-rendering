#version 450


// Kernel group size.
layout (local_size_x = 16, local_size_y = 16) in;
// Output image.
layout (set = 0, binding = 0, rgba8) uniform image2D outImg;
// Shader basic data.
layout (set = 0, binding = 1) uniform DDAUniforms {
    uvec4 screenResolution;
    vec4 screenWorldSize;

    vec4 cameraPosition;
    vec4 cameraForward;
    vec4 cameraUp;
    vec4 cameraRight;

    vec4 gridWorldCoords;
    uvec4 gridResolution;

    vec4 backgroundColor;
} ddaUniforms;

// Voxels
struct VoxelData {
    vec4 color;
    vec4 normal;
};
layout (std140, set = 0, binding = 2) readonly buffer Voxels {
    VoxelData data[];
} voxels;

// Lights
struct DirectionalLight {
    vec4 color;
    vec4 direction;
};
layout (std140, set = 0, binding = 3) uniform LightBuffer
{
    uvec4 lightCount;
    DirectionalLight lights[8]; // maximum 8 lights for now
} lightBuffer;

// returns 0 if x < 0
//         1 if x > 0
//        (0.5 if x == 0)
float sign01(float x)
{
    return 2.0f * sign(x) - 1.0f;
}

float max3_(float a, float b, float c)
{
    return max(a, max(b, c));
}

float min3_(float a, float b, float c)
{
    return min(a, min(b, c));
}

// A negative value means no intersection.
// A positive value t means the intersection point is at orig + t * dir.
float raygrid_intersect(vec3 orig, vec3 dir)
{
    vec3 invDir = 1.0f / dir;
    vec3 t_0 = (ddaUniforms.gridWorldCoords.xyz - orig) * invDir; 
    vec3 t_1 = (ddaUniforms.gridWorldCoords.xyz + ddaUniforms.gridWorldCoords.www - orig) * invDir;
    
#define SWAP(a, b) { float tmp = a; a = b; b = tmp; }
    // We chech invDir.x < 0f rather than t_0.x > t_1.x (almost equivalent),
    // because of numerical stability issues when dir.x == 0f.
    // See the comment in raytrace() for an explanation.
    if (invDir.x < 0.0f) SWAP(t_0.x, t_1.x)
    if (invDir.y < 0.0f) SWAP(t_0.y, t_1.y)
    if (invDir.z < 0.0f) SWAP(t_0.z, t_1.z)
#undef SWAP

    float t_enter = max3_(t_0.x, t_0.y, t_0.z);
    float t_leave = min3_(t_1.x, t_1.y, t_1.z);

    // no intersection
    if (t_enter > t_leave || t_leave < 0.0f) {
        return -1.0f;
    }
    // the origin is out of the grid.
    if (t_enter > 0.0f) {
        return t_enter;
    }
    // the origin is in the grid.
    else {
        return 0.0f;
    }
}

vec4 shade(ivec3 cell, vec3 ray_dir)
{
    uint index = cell.z +
        ddaUniforms.gridResolution.z * cell.y + 
        ddaUniforms.gridResolution.z * ddaUniforms.gridResolution.y * cell.x; 
    VoxelData voxel = voxels.data[index];

    vec4 diffuse = vec4(0.0f, 0.0f, 0.0f, 0.0f);
    for (uint i = 0; i < lightBuffer.lightCount.x; i++) {
        DirectionalLight light = lightBuffer.lights[i];
        float intensity = max(dot(-light.direction, voxel.normal), 0.0f);
        diffuse += intensity * light.color;
    }

    vec4 ambient = vec4(0.2f, 0.2f, 0.2f, 0.0f);

    return voxel.color * (ambient + diffuse);
}

vec4 raytrace(vec3 orig, vec3 dir)
{
    vec3 cellSize = ddaUniforms.gridWorldCoords.www / ddaUniforms.gridResolution.xyz;
    vec3 normOrig = (orig - ddaUniforms.gridWorldCoords.xyz) / cellSize;
    vec3 invDir = 1.0f / dir;

    // Differential cell/time values.
    ivec3 d_cell = ivec3(sign(dir));
    vec3 d_t = cellSize * abs(invDir);

    // Current cell/time values.
    float curr_t = 0;
    ivec3 curr_cell = ivec3(floor(normOrig));
    
    // Next crossing times.
    // For numerical stability reasons we check the sign of invDir rather than dir.
    // If e.g. dir.x == -0f, then dir.x >= 0f is true but 1f / dir.x = -inf,
    // which is not what we want (when dir.x >= 0f, we want 1f / dir.x >= 0f); 
    vec3 nextCross_t = curr_cell - normOrig;
    if (invDir.x >= 0.0f) nextCross_t.x += 1.0f;
    if (invDir.y >= 0.0f) nextCross_t.y += 1.0f;
    if (invDir.z >= 0.0f) nextCross_t.z += 1.0f;
    nextCross_t *= cellSize * invDir;

    while (true) {
        // Out of grid
        if (curr_cell.x < 0 || curr_cell.x >= ddaUniforms.gridResolution.x || 
            curr_cell.y < 0 || curr_cell.y >= ddaUniforms.gridResolution.y || 
            curr_cell.z < 0 || curr_cell.z >= ddaUniforms.gridResolution.z) {
            return ddaUniforms.backgroundColor;
        }

        // Read the cell color
        uint index = curr_cell.z + 
            ddaUniforms.gridResolution.z * curr_cell.y + 
            ddaUniforms.gridResolution.z * ddaUniforms.gridResolution.y * curr_cell.x; 
        vec4 color = voxels.data[index].color;
        if (color.a >= 0.9f) {
            return shade(curr_cell, dir);
        }

        // x
        if (nextCross_t.x <= nextCross_t.y && nextCross_t.x <= nextCross_t.z) {
            curr_t = nextCross_t.x;
            nextCross_t.x += d_t.x;
            curr_cell.x += d_cell.x;
        }   
        // y
        else if (nextCross_t.y <= nextCross_t.z) {
            curr_t = nextCross_t.y;
            nextCross_t.y += d_t.y;
            curr_cell.y += d_cell.y;
        }
        // z
        else {
            curr_t = nextCross_t.z;
            nextCross_t.z += d_t.z;
            curr_cell.z += d_cell.z;
        }
    }
}

void main()
{
    uvec2 gid = gl_GlobalInvocationID.xy;
    if (gid.x < ddaUniforms.screenResolution.x && 
        gid.y < ddaUniforms.screenResolution.y) {
    
        // Compute the ray direction
        float dx = 2.0f * (gid.x / float(ddaUniforms.screenResolution.x)) - 1.0f;
        float dy = 2.0f * (gid.y / float(ddaUniforms.screenResolution.y)) - 1.0f;
        vec3 dir = normalize(
            ddaUniforms.cameraForward.xyz +
            dx * ddaUniforms.screenWorldSize.x * ddaUniforms.cameraRight.xyz +
            dy * ddaUniforms.screenWorldSize.y * ddaUniforms.cameraUp.xyz);

        float start_t = raygrid_intersect(ddaUniforms.cameraPosition.xyz, dir);
        vec4 color;
        if (start_t > 0.0f) {
            // Project the origin onto the grid, along the ray direction.
            vec3 projected_orig = ddaUniforms.cameraPosition.xyz + start_t * dir + 0.001f * dir;
            color = raytrace(projected_orig, dir);           
        }
        else {
            color = ddaUniforms.backgroundColor;
        }
        imageStore(outImg, ivec2(gid), color);
    }
}