#version 450

// Kernel group size.
layout (local_size_x = 16, local_size_y = 16) in;
 

#extension GL_GOOGLE_include_directive : require
#extension GL_EXT_debug_printf : enable

// Include other files  
//#define DEBUG_LOG
#include "raycasting/utils.comp"
#include "raycasting/inputs.comp"
#include "raycasting/tape.comp"


struct Hit {
    float t;
};


// Returns true if there is an intersection.
// A positive value t means the intersection point is at orig + t * dir.
bool raygrid_intersect(vec3 orig, vec3 dir, out float t_enter, out float t_leave)
{
    vec3 invDir = 1.0f / dir;
    vec3 t_0 = (params_buf.grid_world_pos + vec3(EPS, EPS, EPS) - orig) * invDir; 
    vec3 t_1 = (params_buf.grid_world_pos + params_buf.grid_world_size - vec3(EPS, EPS, EPS) - orig) * invDir;
    
#define SWAP(a, b) { float tmp = a; a = b; b = tmp; }
    // We chech invDir.x < 0f rather than t_0.x > t_1.x (almost equivalent),
    // because of numerical stability issues when dir.x == 0f.
    // See the comment in raytrace() for an explanation.
    if (invDir.x < 0.0f) SWAP(t_0.x, t_1.x)
    if (invDir.y < 0.0f) SWAP(t_0.y, t_1.y)
    if (invDir.z < 0.0f) SWAP(t_0.z, t_1.z)
#undef SWAP

    t_enter = MAX_3(t_0.x, t_0.y, t_0.z);
    t_leave = MIN_3(t_1.x, t_1.y, t_1.z);

    return t_enter <= t_leave && t_leave >= 0.0f;
}


bool dda(vec3 orig, vec3 dir, out Hit hit)
{
    // Check the ray intersects the root node.
    float grid_t_enter;
    float grid_t_leave;
    bool hit_grid = raygrid_intersect(orig, dir, grid_t_enter, grid_t_leave);
    if (!hit_grid) {
        return false;
    }

    // DDA parameters. 
    vec3 invDir = 1.0f / dir;
    uint lvl;
    uint node;
    float t_curr = max(grid_t_enter, 0.0f) + EPS; // the current time
    float t_max = t_curr;

    //LOG2("\nt_enter=%.3f t_leave=%.3f\n", grid_t_enter, grid_t_leave);
   
    // We use the kd-restart algorithm.
    while (t_max < grid_t_leave - EPS) {
        lvl = 0;
        node = 0;
        t_curr = t_max + EPS;
        t_max = grid_t_leave;

        while (t_curr < t_max - EPS) {
            // Prepare
            vec3 norm_pos = (orig + (t_curr + EPS) * dir - node_world_pos(node, lvl)) /
                params_buf.levels[lvl].cell_size; 
            ivec3 cell = ivec3(floor(norm_pos));
            vec3 t_next = (t_curr + EPS) + (cell + SIGN_01(invDir) - norm_pos) * 
                params_buf.levels[lvl].cell_size * invDir;
            uint cell_index = INDEX3D(cell, params_buf.levels[lvl].dim);

            //LOG8("level=%u   node=%u   pos=%.3v3f   cell_size=%.4f   norm_pos=%.3v3f   cell=%v3d   cell_index=%u   node_pos=%.3v3f\n", 
            //    lvl, node, orig + (t_curr + EPS) * dir, params_buf.levels[lvl].cell_size, norm_pos, cell, cell_index, node_world_pos(node, lvl));
     
            if (node_has_leaf_child(node, lvl, cell_index)) {
                hit.t = t_curr;  
                //LOG0("Hit voxel\n");
                return true;
            }
            // Recurse in the child
            else if (node_has_interior_child(node, lvl, cell_index)) {
                //LOG0("Recursing in child\n");
                node = node_get_interior_child(node, lvl, cell_index);
                t_max = MIN_3(t_next.x, t_next.y, t_next.z);
                lvl++;
            }
            // No child : step forward 
            else {
                //LOG0("Stepping\n");
                t_curr = MIN_3(t_next.x, t_next.y, t_next.z);
            }
        }
    }
    // We didn't hit anything
    return false;
}

vec3 tape_normal(vec3 pos)
{
    float d = eval_tape(pos);
    float dx = eval_tape(pos + vec3(EPS, 0, 0));
    float dy = eval_tape(pos + vec3(0, EPS, 0));
    float dz = eval_tape(pos + vec3(0, 0, EPS));

    return normalize(vec3((dx - d) / EPS, (dy - d) / EPS, (dz - d) / EPS));
}


vec4 shade(vec3 ray_orig, vec3 ray_dir, Hit hit)
{
    vec3 normal = tape_normal(ray_orig + hit.t * ray_dir);
    
    vec4 diffuse = vec4(0.0f, 0.0f, 0.0f, 0.0f);
    for (uint i = 0; i < params_buf.light_count; i++) {
        DirectionalLight light = params_buf.lights[i];
        
        // Check the light illuminates the voxel
        //vec3 shadow_ray_orig = ray_orig + (hit.t - EPS) * ray_dir; 
        //Hit dummy_hit;
        //bool did_hit = dda(shadow_ray_orig, -light.direction.xyz, dummy_hit);
        //if (!did_hit) {
            // Add the light contribution

            float intensity = max(dot(-light.direction.xyz, normal), 0.0f);
            diffuse += intensity * light.color;
        //}
    }

    vec4 ambient = vec4(0.2f, 0.2f, 0.2f, 1.0f);

    return params_buf.materials[0].color * (ambient + diffuse);
}



vec4 raytrace(vec3 orig, vec3 dir)
{
    Hit hit;
    bool did_hit = dda(orig, dir, hit);
    if (did_hit) {
        // Something went wrong : show a debug color.
        if (hit.t < 0.0f) {
            return vec4(1, 1, 1, 1);
        }
        // All is good.
        return shade(orig, dir, hit);
    }
    else {
        return params_buf.background_color;
    }
}


void main()
{
    uvec2 gid = gl_GlobalInvocationID.xy;

    if (gl_GlobalInvocationID.x == 0 && gl_GlobalInvocationID.y == 0) {
        debugPrintfEXT("\n");
    }

    if (gid.x < params_buf.screen_res.x && 
        gid.y < params_buf.screen_res.y) 
    {
        // Compute the ray direction
        float dx = 2.0f * (gid.x / float(params_buf.screen_res.x)) - 1.0f;
        float dy = 2.0f * (gid.y / float(params_buf.screen_res.y)) - 1.0f;
        vec3 dir = normalize(
            params_buf.camera_forward.xyz +
            dx * params_buf.screen_world_size.x * params_buf.camera_right.xyz +
            dy * params_buf.screen_world_size.y * params_buf.camera_up.xyz);

        vec4 color = raytrace(params_buf.camera_pos.xyz, dir);
        imageStore(out_img, ivec2(gid), color);
    }
}